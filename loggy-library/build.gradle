apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'com.google.protobuf'
apply plugin: 'maven-publish'

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.1"

    defaultConfig {
        minSdkVersion 26
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"

        buildConfigField("String", "loggyUrl", "\"http://78.46.17.17:50111\"")
        buildConfigField("int", "logPriority", "android.util.Log.VERBOSE")
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }

        localhost {
            initWith debug
            buildConfigField("String", "loggyUrl", "\"http://10.0.2.2:50111\"")
            buildConfigField("int", "logPriority", "android.util.Log.VERBOSE")
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }

    buildFeatures {
        viewBinding = true
    }

    sourceSets["main"].java.srcDir("src/main/kotlin")
}

protobuf {
    protoc { artifact = 'com.google.protobuf:protoc:3.12.2' }
    plugins {
        grpc {
            artifact = 'io.grpc:protoc-gen-grpc-java:1.31.1' // CURRENT_GRPC_VERSION
        }
        grpckt {
            artifact = "io.grpc:protoc-gen-grpc-kotlin:0.1.5"
        }
    }
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java { option 'lite' }
            }
            task.plugins {
                grpc { // Options added to --grpc_out
                    option 'lite'
                }
                grpckt { // Options added to --grpc_out
                    option 'lite'
                }
            }
        }
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-serialization-core:1.0.0-RC"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.4.1"

    //logging
    implementation "com.jakewharton.timber:timber:4.7.1"


    implementation "javax.annotation:javax.annotation-api:1.3.2"
    implementation 'io.grpc:grpc-android:1.31.1'
    runtimeOnly 'io.grpc:grpc-okhttp:1.31.1'
    implementation 'io.grpc:grpc-protobuf-lite:1.31.1'
    implementation "io.grpc:grpc-stub:1.31.1"
    implementation("io.grpc:grpc-kotlin-stub:0.1.5") {
        exclude group: "io.grpc"
        exclude group: "grpc-protobuf"
    }

    testImplementation 'junit:junit:4.13'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'

}

// Because the components are created only during the afterEvaluate phase, you must
// configure your publications using the afterEvaluate() lifecycle method.
afterEvaluate {
    publishing {
        publications {
            // Creates a Maven publication called "release".
            release(MavenPublication) {
                // Applies the component for the release build variant.
                from components.release

                // You can then customize attributes of the publication as shown below.
                groupId = 'sh.loggy'
                artifactId = 'final'
                version = '0.1'
            }
            // Creates a Maven publication called “debug”.
            debug(MavenPublication) {
                // Applies the component for the debug build variant.
                from components.debug

                groupId = 'sh.loggy'
                artifactId = 'final-debug'
                version = '0.1'
            }
        }
    }
}